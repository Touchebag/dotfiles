#
# ~/.zshrc

# If not running interactively, don't do anything
[[ $- != *i* ]] && return

# Vim mode
set -o vi

# Hstory options
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_SAVE_NO_DUPS
setopt INC_APPEND_HISTORY
setopt SHARE_HISTORY
setopt HIST_REDUCE_BLANKS

# Persistent history
HISTFILE="$HOME/.zsh_history"
HISTSIZE=1000
SAVEHIST=$HISTSIZE

# Completion magic
autoload -Uz compinit
compinit

# Glob magic
setopt extendedglob

# Vim auto-completion rules
zstyle ':completion:*:*:vim:*' \
ignored-patterns '*.aux' '*.log' '*.pdf' '*.fdb_latexmk' '*.fls' '*.toc'\
  '*.bcf' '*.blg' '*.run.xml' '*.bbl' \
  '*.hi' '*.o' '*.dyn_hi' '*.dyn_o'

# Aliases {{{

# Feel good alias for sudo
alias fucking='sudo'

# Dir listings
alias ls='ls --color=auto'
alias la='ls -A'
alias ll='ls -lA'

# Ease of use
alias cd..='cd ..'
alias u='cd ..'
alias y='yaourt'

# Git
alias g='git'
alias ga='git add'
alias gb='git branch'
alias gc='git commit'
alias gca='git commit -a'
alias gch='git checkout'
alias gd='git diff'
alias gf='git fetch'
alias gph='git push'
alias gpl='git pull'
alias gr='git rebase'
alias grc='git rebase --continue'
alias gre='git-review'
alias gs='git status'

#}}}

# For 'fuck' script to work
eval $(thefuck --alias)

autoload -Uz vcs_info
setopt prompt_subst
precmd () {
vcs_info
zstyle ':vcs_info:git*' formats "%b"
STATUS=$(command git status --porcelain 2> /dev/null | tail -n1)
if [[ -n ${vcs_info_msg_0_} ]]; then
  if [[ -n $STATUS ]]; then
    gitbranch=' (%F{red}${vcs_info_msg_0_}%F{white})'
    else
    gitbranch=' (%F{green}${vcs_info_msg_0_}%F{white})'
  fi
else
  gitbranch=''
fi
}

# Displays vi mode in prompt
function zle-line-init zle-keymap-select {
    text1=$'[%{\e[38;5;040m%}%2~%{\e[0m%}]'
    vimode="${${KEYMAP/vicmd/ --NORMAL--}/(main|viins)/ --INSERT--}"
    text2=$'\n%{\e[38;5;014m%}  >> %{\e[0m%}'
    PROMPT=$text1$gitbranch$vimode$text2
    case $KEYMAP in
      vicmd) echo -e -n "\x1b[\x32 q" ;;
      main|viins) echo -e -n "\x1b[\x36 q" ;;
    esac
    zle reset-prompt
}
zle -N zle-line-init
zle -N zle-keymap-select

# Automatically run ls after cd
function chpwd() {
  emulate -L zsh
  ls -A
}

# Autocomplete from history with Up/Down keys
bindkey "^[[A" history-beginning-search-backward
bindkey "^[[B" history-beginning-search-forward

